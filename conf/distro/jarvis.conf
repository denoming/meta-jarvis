DISTRO_NAME = "Jarvis (J.A.R.V.I.S Embedded Linux Distro)"
DISTRO_VERSION = "0.1"
SDK_VENDOR = "-jarsdk"
SDK_VERSION = "${@d.getVar('DISTRO_VERSION').replace('snapshot-${METADATA_REVISION}', 'snapshot')}"
SDK_VERSION[vardepvalue] = "${SDK_VERSION}"

MAINTAINER = "Denys Asauliak <d.asauliak@google.com>"

TARGET_VENDOR = "-jarvis"

LOCALCONF_VERSION = "2"

JAR_DEFAULT_DISTRO_FEATURES = "largefile multiarch virtualization"
JAR_DEFAULT_EXTRA_RDEPENDS = "packagegroup-core-boot"
JAR_DEFAULT_EXTRA_RRECOMMENDS = "kernel-module-af-packet"

SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
SDKPATHINSTALL = "/opt/${DISTRO}/${SDK_VERSION}"

require conf/distro/include/no-static-libs.inc
require conf/distro/include/yocto-uninative.inc
require conf/distro/include/security_flags.inc
INHERIT += "uninative"

BB_SIGNATURE_HANDLER ?= "OEEquivHash"
BB_HASHSERVE ??= "auto"

TCLIBCAPPEND = ""

INIT_MANAGER = "systemd"

DISTRO_FEATURES ?= "${DISTRO_FEATURES_DEFAULT} ${JAR_DEFAULT_DISTRO_FEATURES}"
DISTRO_EXTRA_RDEPENDS += "${JAR_DEFAULT_EXTRA_RDEPENDS}"
DISTRO_EXTRA_RRECOMMENDS += "${JAR_DEFAULT_EXTRA_RRECOMMENDS}"

# Remove X11 and 3G features by default
DISTRO_FEATURES:remove = "x11 3g"

# We need debug symbols so that SPDX license manifests for the kernel work
KERNEL_EXTRA_FEATURES:append = " features/debug/debug-kernel.scc"
